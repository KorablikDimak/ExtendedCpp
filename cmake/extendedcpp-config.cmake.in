macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/extendedcpp-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "Events ${type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/extendedcpp-${type}-targets.cmake")
endmacro()

if(NOT TARGET ExtendedCpp::LINQ
        OR NOT TARGET ExtendedCpp::Events
        OR NOT TARGET ExtendedCpp::Json
        OR NOT TARGET ExtendedCpp::Xml
        OR NOT TARGET ExtendedCpp::InfoLog
        OR NOT TARGET ExtendedCpp::Common)
    set(type "")

    if(DEFINED ExtendedCpp_SHARED_LIBS)
        if(ExtendedCpp_SHARED_LIBS)
            set(type "shared")
        else()
            set(type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/extendedcpp-shared-targets.cmake")
        set(type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/extendedcpp-static-targets.cmake")
        set(type "static")
    else()
        set(type "shared")
    endif()

    import_targets(${type})
    check_required_components(ExtendedCpp)
    message("-- Found ${type} ExtendedCpp (version ${${CMAKE_FIND_PACKAGE_NAME}_VERSION})")
endif()